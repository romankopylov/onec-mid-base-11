#Область ПрограммныйИнтерфейс

// Дополняет переданную форму элементами.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма документа, в которую добавляются элементы 
//										в зависимости от типа документа.
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДополнитьФормуДокументаЗаказПокупателя(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда 
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);	
	КонецЕсли;		
	
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	Поле = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	Поле.Вид = ВидПоляФормы.ПолеВвода;
	Поле.ПутьКданным = "Объект.НЕТ_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)
	
	Поле = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.СуммаДокумента);
	Поле.Вид = ВидПоляФормы.ПолеВвода;
	Поле.ПутьКданным = "Объект.НЕТ_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоПередОснование(Форма)
	
	Поле = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.Основание);
	Поле.Вид = ВидПоляФормы.ПолеВвода;
	Поле.ПутьКданным = "Объект.НЕТ_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДополнитьФормуДокументаЗаказПокупателя(Форма)
	
	Поле = Форма.Элементы.Добавить("Статус", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Поле.Вид = ВидПоляФормы.ПолеВвода;
	Поле.ПутьКданным = "Объект.НЕТ_Статус";
	
	Группа = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	
	Поле = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	Поле.Вид = ВидПоляФормы.ПолеВвода;
	Поле.ПутьКданным = "Объект.НЕТ_СогласованнаяСкидка";
	Поле.УстановитьДействие("ПриИзменении", "Подключаемый_СогласованнаяСкидкаПриИзменении");
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "ПересчитатьТаблицу";
	
	Кнопка = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	Кнопка.ИмяКоманды = "ПересчитатьТаблицу";
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	Кнопка.Картинка = БиблиотекаКартинок.Обновить;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
КонецПроцедуры 

#КонецОбласти